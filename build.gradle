plugins {
  id 'com.github.johnrengelman.shadow' version '2.0.4'
  id 'java'
  id 'maven'
  id 'maven-publish'
  id 'application'
  id 'com.jfrog.bintray' version '1.8.4'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'org.apache.avro'
version = '1.8.2-1'

repositories {
  mavenCentral()
}

bintray {
  user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
  key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
  publications = ['application']
  pkg {
    repo = 'maven-oss-releases'
    name = 'avro-tools-aws'
    userOrg = 'segence'
    licenses = ['Apache-2.0']
    vcsUrl = 'https://github.com/segence/avro-tools-aws.git'
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = gradleWrapperVersion
}

task printVersion {
  doLast {
    println project.version
  }
}

dependencies {
  compile (
    "org.apache.hadoop:hadoop-aws:2.8.0",
    "org.apache.hadoop:hadoop-common:2.8.0",
    "org.apache.hadoop:hadoop-annotations:2.8.0",
    "org.apache.hadoop:hadoop-auth:2.8.0",
    "org.apache.hadoop:hadoop-client:2.8.0",
    "org.apache.hadoop:hadoop-hdfs:2.8.0",
    "com.amazonaws:aws-java-sdk-core:1.10.6",
    "com.amazonaws:aws-java-sdk-s3:1.10.6",
    "com.amazonaws:aws-java-sdk-kms:1.10.6",
    "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.9.4"
  )
  compile("org.apache.avro:avro-tools:${avroVersion}") {
    exclude group: 'org.apache.commons'
    exclude group: 'commons-beanutils'
    exclude group: 'org.apache.hadoop'
  }
}

mainClassName = 'org.apache.avro.tool.Main'

shadowJar {
  classifier = null
  baseName = 'avro-tools-aws'
}

publishing {
  publications {
    application(MavenPublication) { publication ->
      project.shadow.component(publication)
    }
  }
}
